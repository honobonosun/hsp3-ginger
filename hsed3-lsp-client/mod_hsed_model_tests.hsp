// LICENSE: CC0-1.0
// v0

#include "mod_hsed_model.hsp"

#module m_hsed_model_tests

#const true 1
#const false 0

#deffunc hsed_model_tests_init

	dim s_tab_count
	dim s_footy_ids
	sdim s_file_paths
	sdim s_texts
	dim s_text_lens

	dim s_document_ids
	dim s_document_tab_ids
	dim s_document_count

	dim s_event_kinds
	sdim s_document_uris
	dim s_event_count
	return

#deffunc hsed_model_tests_diffing_setup

	hsed_model_init
	hsed_model_tests_init

	// 初期状態
	s_tab_count = 2
	s_footy_ids = 1, 2
	s_file_paths = "C:/1.hsp", "C:/2.hsp"
	s_texts = "mes 1", "mes 222"
	s_text_lens = strlen(s_texts(0)), strlen(s_texts(1))
	hsed_model_update_tabs s_tab_count, s_footy_ids, s_file_paths, s_text_lens

	// 2つのファイルはどちらも dirty のはず。
	hsed_model_emit_dirty_documents s_document_ids, s_document_tab_ids, s_document_count
	assert s_document_count == 2
	assert s_document_ids(0) != s_document_ids(1)
	assert s_document_tab_ids(0) == 0
	assert s_document_tab_ids(1) == 1

	// テキストを設定する。
	repeat s_document_count
		hsed_model_set_document_text s_document_ids(cnt), s_texts(s_document_tab_ids(cnt))
	loop

	// opened イベントが観測されるはず。
	hsed_model_emit_document_changes s_event_kinds, s_document_uris, s_event_count
	assert s_event_count == 2
	assert s_event_kinds(0) == hsed_model_k_document_opened
	assert s_document_uris(0) == "file:///C:/1.hsp"
	assert s_event_kinds(1) == hsed_model_k_document_opened
	assert s_document_uris(1) == "file:///C:/2.hsp"
	return

#deffunc hsed_model_tests_no_diff

	hsed_model_tests_diffing_setup

	// 状態が変化しなければイベントが発生しないことを確認する。
	hsed_model_update_tabs s_tab_count, s_footy_ids, s_file_paths, s_text_lens

	hsed_model_emit_dirty_documents s_document_ids, s_document_tab_ids, s_document_count
	assert s_document_count == 0

	hsed_model_emit_document_changes s_event_kinds, s_document_uris, s_event_count
	assert s_event_count == 0
	return

#deffunc hsed_model_tests_one_opened_document

	hsed_model_tests_diffing_setup

	// 新しいタブを開く。
	s_footy_ids(s_tab_count) = 3
	s_file_paths(s_tab_count) = "C:/3.hsp"
	s_texts(s_tab_count) = "mes 33333"
	s_text_lens(s_tab_count) = strlen(s_texts(s_tab_count))
	s_tab_count++

	hsed_model_update_tabs s_tab_count, s_footy_ids, s_file_paths, s_text_lens

	// opened イベントが観測されるはず。
	hsed_model_emit_document_changes s_event_kinds, s_document_uris, s_event_count
	assert s_event_count == 1
	assert s_event_kinds(0) == hsed_model_k_document_opened
	assert s_document_uris(0) == "file:///C:/3.hsp"
	return

#deffunc hsed_model_tests_one_changed_document

	hsed_model_tests_diffing_setup

	// テキストの長さを変更する。
	s_texts(1) = "print 22222"
	s_text_lens(1) = strlen(s_texts(1))

	hsed_model_update_tabs s_tab_count, s_footy_ids, s_file_paths, s_text_lens

	// dirty になるはず。
	hsed_model_emit_dirty_documents s_document_ids, s_document_tab_ids, s_document_count
	assert s_document_count == 1
	assert s_document_tab_ids(0) == 1

	// テキストを設定する。
	hsed_model_set_document_text s_document_ids(0), s_texts(s_document_tab_ids(0))

	// changed イベントが観測されるはず。
	hsed_model_emit_document_changes s_event_kinds, s_document_uris, s_event_count
	assert s_event_count == 1
	assert s_event_kinds(0) == hsed_model_k_document_changed
	assert s_document_uris(0) == "file:///C:/2.hsp"
	return

#deffunc hsed_model_tests_one_closed_document

	hsed_model_tests_diffing_setup

	// タブを閉じる。
	s_tab_count--

	hsed_model_update_tabs s_tab_count, s_footy_ids, s_file_paths, s_text_lens

	// closed イベントが観測されるはず。
	hsed_model_emit_document_changes s_event_kinds, s_document_uris, s_event_count
	assert s_event_count == 1
	assert s_event_kinds(0) == hsed_model_k_document_closed
	assert s_document_uris(0) == "file:///C:/2.hsp"
	return

#deffunc hsed_model_tests_nonfile_tabs

	hsed_model_init

	s_tab_count = 1
	s_footy_ids = 1
	s_file_paths = ""
	s_texts = "mes 1"
	s_text_lens = strlen(s_texts(0))
	hsed_model_update_tabs s_tab_count, s_footy_ids, s_file_paths, s_text_lens

	hsed_model_emit_document_changes s_event_kinds, s_document_uris, s_event_count
	assert s_event_count == 1
	assert s_event_kinds(0) == hsed_model_k_document_opened
	assert s_document_uris(0) == "hsed:///footies/1/"

	hsed_model_update_tabs s_tab_count, s_footy_ids, s_file_paths, s_text_lens
	hsed_model_emit_document_changes s_event_kinds, s_document_uris, s_event_count
	assert s_event_count == 0
	return

#deffunc hsed_model_tests_dirtyness

	return

#deffunc hsed_model_tests_main

	hsed_model_tests_init

	hsed_model_tests_no_diff
	hsed_model_tests_one_opened_document
	hsed_model_tests_one_changed_document
	hsed_model_tests_one_closed_document
	hsed_model_tests_nonfile_tabs

	hsed_model_tests_dirtyness
	return

#global

	hsed_model_tests_main

	mes "passed"
	await 500
	end
	end
