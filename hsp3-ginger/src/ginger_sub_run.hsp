// スクリプト実行機能

#module m_ginger_sub_run

#include "hspcmp.as"

#define true 1
#define false 0

#deffunc ginger_sub_run_main var args, int argc, int show_help, var error

	sdim s_src_path

	if show_help {
		ginger_sub_help_main args, argc, show_help, error
		return stat
	}

	ginger_sub_run_parse_args args, argc, error
	if stat == false {
		return false
	}

	ginger_sub_run_run error
	return stat

#deffunc ginger_sub_run_parse_args array args, int argc, var error, \
	local i, local ok

	ok = true

	repeat
		if i >= argc {
			break
		}

		if args(i) == "--" {
			break
		}

		if s_src_path_exists == false {
			s_src_path = args(i)
			s_src_path_exists = true
			i++
			continue
		}

		error = "不明な引数: " + args(i)
		ok = false
		break
	loop

	if ok == false {
		return false
	}

	if s_src_path_exists == false {
		error = "スクリプトファイルを指定してください。"
		return false
	}

	return true

#deffunc ginger_sub_run_run var error, \
	local hsp_dir, local obj_name, local runtime_name, local runtime_path, local cmdline

	ginger_core_get_hsp_dir hsp_dir
	if stat == false {
		error = "HSP のインストールディレクトリを指定してください。"
		return false
	}

	ginger_core_compile s_src_path
	if stat == false {
		ginger_core_getmes error
		return false
	}

	ginger_core_get_runtime_name runtime_name

	runtime_path = hsp_dir + "\\" + runtime_name

	ginger_core_get_obj_name obj_name

	// FIXME: 引数を渡す
	// FIXME: 標準入出力を継承する
	cmdline = "\"" + runtime_path + "\" \"" + obj_name + "\""
	hsc3_run cmdline
	return true

#global
